cosmwasm_optimizer_version := "0.16.0"

# regenerate lib
lib:
  #!/usr/bin/env bash -e
  cd packages/lib
  cargo build

# regenerate bindings schema
bindings:
  #!/usr/bin/env bash -e
  cd packages/bindings
  cargo build
  cargo run schema

# regenerate schema for the specified contract
schema contract:
  #!/usr/bin/env bash 
  cd contracts/{{contract}}
  cargo build
  cargo run schema

# compile a contract into a WASM binary (default: compiles all contracts)
compile path="":
  #!/usr/bin/env bash -e
  docker run --rm -v "$(pwd)":/code \
    --platform=linux/amd64 \
    --mount type=volume,source="$(basename "$(pwd)")_cache",target=/target \
    --mount type=volume,source=registry_cache,target=/usr/local/cargo/registry \
    cosmwasm/workspace-optimizer:{{cosmwasm_optimizer_version}} \
    {{ if path != "" { "./contracts" / path } else { "." } }}
